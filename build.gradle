import com.diffplug.gradle.spotless.SpotlessTask

/*
 * Licensed under the Apache License, Version 2.0 (the "License") you may not use this file except
 * in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0 Unless required by applicable law or agreed to in
 * writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific
 * language governing permissions and limitations under the License.
 */

plugins {
  id 'eclipse'
  id 'idea'
  id 'java-library'
  id 'maven-publish'
  id 'signing'
  id 'com.diffplug.spotless' version '8.2.0'
  id 'com.google.protobuf' version '0.9.6'
  id 'net.ltgt.errorprone' version '4.4.0'
}

group = 'net.sf.xmldb-org'

repositories {
  mavenCentral()
  maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
  mavenLocal()
}

java {
  sourceCompatibility = JavaVersion.VERSION_21
  withJavadocJar()
  withSourcesJar()
}

sourceSets.test {
  java { srcDir 'src/remoteTest/java' }
}

configurations {
  tckTests {
    canBeConsumed = false
    canBeResolved = true
    extendsFrom testImplementation, testRuntimeOnly
  }
}

configurations.configureEach {
  resolutionStrategy {
    cacheChangingModulesFor(0, 'seconds')
    cacheDynamicVersionsFor(0, 'seconds')
  }
}

dependencies {
  implementation 'ch.qos.logback:logback-classic:1.5.25'
  implementation 'com.google.protobuf:protobuf-javalite:4.33.4'
  implementation 'io.grpc:grpc-stub:1.78.0'
  implementation 'io.grpc:grpc-protobuf:1.78.0'
  implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
  implementation 'net.sf.xmldb-org:xmldb-api:3.0.0-SNAPSHOT'

  tckTests  'net.sf.xmldb-org:xmldb-api-tck:0.1.0-SNAPSHOT'

  // used for server / client
  implementation 'io.grpc:grpc-netty-shaded:1.78.0'

  errorprone 'com.google.errorprone:error_prone_core:2.46.0'

  testImplementation 'net.sf.xmldb-org:xmldb-api-tck:0.1.0-SNAPSHOT'
  testImplementation 'net.datafaker:datafaker:2.5.3'
  testImplementation 'org.assertj:assertj-core:3.27.6'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.2'
  testImplementation 'org.mockito:mockito-core:5.21.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:5.21.0'

  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.2'
  testRuntimeOnly 'org.junit.platform:junit-platform-engine:6.0.2'
  testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.14.2'
  testRuntimeOnly 'org.junit.platform:junit-platform-console:6.0.2'
  testRuntimeOnly 'org.mockito:mockito-core:5.21.0'
}

compileJava {
  options.errorprone {
      excludedPaths = '.*/build/generated/sources/proto/.*'
      allErrorsAsWarnings = false
      disableWarningsInGeneratedCode = true
  }
}

spotless {
  java {
    eclipse().configFile('gradle/xmldb-remote-style.xml')
    importOrderFile('gradle/xmldb-remote.importorder')
    licenseHeaderFile('gradle/LICENSE_HEADER_java')
    replace('generated', '@javax.annotation.Generated', '@jakarta.annotation.Generated')
  }
}

protobuf {
  protoc {
    // The artifact spec for the Protobuf Compiler
    artifact = 'com.google.protobuf:protoc:3.25.8'
  }
  plugins {
    // Optional: an artifact spec for a protoc plugin, with "grpc" as
    // the identifier, which can be referred to in the "plugins"
    // container of the "generateProtoTasks" closure.
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.78.0'
    }
  }
  generateProtoTasks {
    ofSourceSet('main').configureEach {
      plugins {
        // Apply the "grpc" plugin whose spec is defined above, without
        // options.  Note the braces cannot be omitted, otherwise the
        // plugin will not be added. This is because of the implicit way
        // NamedDomainObjectContainer binds the methods.
        grpc {}
      }
    }
  }
}

tasks.withType(Jar).configureEach {
  dependsOn 'generateProto'
}

tasks.withType(JavaCompile).configureEach {
  dependsOn 'spotlessJavaApply'
  options.compilerArgs += ['-Xlint:unchecked', '-XDaddTypeAnnotationsToSymbol=true']
}

tasks.withType(SpotlessTask).configureEach {
  mustRunAfter 'generateProto'
}

tasks.withType(Test).configureEach {
  testLogging {
    events 'skipped'
  }
}

tasks.named('test') {
  useJUnitPlatform()
}

tasks.register('protobuf', Jar) {
  archiveClassifier = 'proto'
  from 'src/main/proto'
}

tasks.register('runTck', JavaExec) {
  group = 'verification'
  description = 'Runs the TCK tests from the tck source set (for TCK authors)'
  mainClass = 'org.junit.platform.console.ConsoleLauncher'
  classpath = files(configurations.tckTests, sourceSets.test.runtimeClasspath)
  args    'execute',
          '--details=tree',
          '--select-package=org.xmldb.tck.tests',
          '--reports-dir',
          "${layout.buildDirectory.dir('tck-reports').get().getAsFile()}"
}

jar {
  manifest.attributes 'Implementation-Title': 'XML:DB gRPC client',
                      'Automatic-Module-Name': "org.xmldb.grpc.client"
}

javadoc {
  excludes = ['org/xmldb/api/grpc/**']
}

publishing {
  repositories {
    maven {
      name = 'Sonatype'
      def releasesRepoUrl = 'https://ossrh-staging-api.central.sonatype.com/service/local/'
      def snapshotsRepoUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
      url = version.toString().endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

      credentials {
        username = findProperty('sonatypeUsername') ?: System.getenv('SONATYPE_USERNAME')
        password = findProperty('sonatypePassword') ?: System.getenv('SONATYPE_PASSWORD')
      }
    }
  }
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact tasks.protobuf

      pom {
        name = 'XML:DB gRPC client'
        url = 'https://github.com/xmldb-org'
        description = 'XML:DB gRPC client implementation'

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'reinhapa'
            name = 'Patrick Reinhart'
            email = 'patrick@reini.net'
          }
          developer {
            id = 'ohumbel'
            name = 'Otmar Humbel'
            email = 'per_nyfelt@users.sf.net'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/xmldb-org/xmldb-grpc-client.git'
          developerConnection = 'scm:git:ssh://github.com/xmldb-org/xmldb-grpc-client.git'
          url = 'https://github.com/xmldb-org/xmldb-grpc-client'
        }
      }
    }
  }
}
signing {
  required = !version.toString().endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish")
  sign publishing.publications.mavenJava
}

normalization {
  runtimeClasspath {
    ignore '/META-INF/MANIFEST.MF'
  }
}
